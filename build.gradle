plugins {
    id "org.jetbrains.kotlin.jvm" version "1.1.60"
    // need to use Gretty here because of https://github.com/johndevs/gradle-vaadin-plugin/issues/317
    id "org.akhikhl.gretty" version "2.0.0"
    id 'com.devsoap.plugin.vaadin' version '1.2.4'
}

gretty {
    contextPath = '/'
}

vaadin {
    version '8.1.6'
}

defaultTasks 'clean', 'build'

repositories {
    maven { url "https://dl.bintray.com/mvysny/vaadin-on-kotlin" }
}

dependencies {
    // Karibu-DSL dependency
    compile 'com.github.vok.karibudsl:karibu-dsl-v8:0.2.14'

    // include proper kotlin version
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.60'

    // workaround until https://youtrack.jetbrains.com/issue/IDEA-178071 is fixed
    compile "com.vaadin:vaadin-themes:${vaadin.version}"
    compile "com.vaadin:vaadin-client-compiled:${vaadin.version}"

    // since we're using async stuff, we need to push updated UI to the client
    compile "com.vaadin:vaadin-push:${vaadin.version}"
    // adds support for cancelable coroutines
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19.3'
    // a http client which does not block
    compile 'org.asynchttpclient:async-http-client:2.0.37'

    compile 'ch.qos.logback:logback-classic:1.2.3'

    // simple REST support so that we can test the client
    compile 'org.jboss.resteasy:resteasy-servlet-initializer:3.1.3.Final'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.61"
    // need to use Gretty here because of https://github.com/johndevs/gradle-vaadin-plugin/issues/317
    id "org.gretty" version "2.1.0"
    id 'com.devsoap.plugin.vaadin' version '1.3.1'
}

gretty {
    contextPath = '/'
}

vaadin {
    version '8.5.1'
}

defaultTasks 'clean', 'build'

repositories {
    maven { url "https://dl.bintray.com/mvysny/github" }
}

dependencies {
    // Karibu-DSL dependency
    compile 'com.github.vok.karibudsl:karibu-dsl-v8:0.4.7'

    // include proper kotlin version
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    // workaround until https://youtrack.jetbrains.com/issue/IDEA-178071 is fixed
    compile "com.vaadin:vaadin-themes:${vaadin.version}"
    compile "com.vaadin:vaadin-client-compiled:${vaadin.version}"

    // since we're using async stuff, we need to push updated UI to the client
    compile "com.vaadin:vaadin-push:${vaadin.version}"
    // adds support for cancelable coroutines
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    // a http client which does not block
    compile 'org.asynchttpclient:async-http-client:2.0.37'

    compile 'ch.qos.logback:logback-classic:1.2.3'

    // simple REST support so that we can test the client
    compile 'org.jboss.resteasy:resteasy-servlet-initializer:3.1.3.Final'

    // heroku app runner
    testRuntime 'com.github.jsimone:webapp-runner:8.5.30.0'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// Heroku
task stage(dependsOn: ['build'])
task copyToLib(type: Copy) {
    into "$buildDir/server"
    from(configurations.testRuntime) {
        include "webapp-runner*"
    }
}

stage.dependsOn(copyToLib)
